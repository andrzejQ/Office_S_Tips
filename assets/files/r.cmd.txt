@echo off
chcp 65001> nul
set DUZY=4000000
::folder i_view32.INI lub i_view64.INI tam gdzie jest r.cmd:
set vINI=%~dp0
echo Wpisz r.cmd w pasku eksploratora plików - nastąpi 50% zmniejszenie DUŻYCH *.jpg
echo.
echo Konwersja *.jpg w podfolderach "%CD%"
IF not defined ProgramFiles(x86) set ProgramFiles(x86)=%ProgramFiles%& REM przypadek sys.op.32bit
set "ProgrIrfanView=%ProgramFiles(x86)%\IrfanView\i_view32.exe"
IF not exist "%ProgrIrfanView%" set "ProgrIrfanView=%ProgramFiles%\IrfanView\i_view64.exe"& REM sys.op.64bit
echo %ProgrIrfanView%...

FOR /R "%CD%" %%G IN (*.jpg) DO (call :subroutine "%%G")
GOTO :end

:subroutine
  IF %~z1 GEQ %DUZY% (
    echo DUŻY: %1....%~z1.
    "%ProgrIrfanView%" %1 /ini="%vINI%" /advancedbatch /convert=%1
    IF ERRORLEVEL 1 echo Nie jest dobrze....& pause
  )
  GOTO :eof

:end
pause
::timeout 10


